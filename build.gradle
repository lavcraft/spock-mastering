buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}

plugins {
    id 'nebula.nebula-release' version '2.2.5'
    id "io.spring.dependency-management" version "0.5.2.RELEASE"
}

allprojects {
    version = '1.0.0'
    group = 'lavcraft.test.spock'

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'

    dependencyManagement {
        dependencies {
            dependency 'org.projectlombok:lombok:1.16.4'
            dependency 'org.codehaus.groovy:groovy-all:2.4.4'
            dependency 'io.fastjson:boon:0.30'

            dependency 'org.slf4j:slf4j-api:1.7.12'
            dependency 'ch.qos.logback:logback-classic:1.1.2'

            dependencySet(group: 'org.spockframework', version: '1.0-groovy-2.4') {
                entry 'spock-core'
                entry 'spock-spring'
            }
            dependency 'junit:junit:4.11'
            dependency 'cglib:cglib:3.1'
            dependency 'com.github.tomakehurst:wiremock:1.57'
            dependency 'com.github.stefanbirkner:system-rules:1.12.1'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    dependencies {
        provided "org.projectlombok:lombok"
        compile 'org.codehaus.groovy:groovy-all'
        compile 'org.slf4j:slf4j-api'

        testCompile 'junit:junit'
        testCompile 'org.spockframework:spock-core'
    }
}

repositories {
    mavenLocal()
    jcenter()
}

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}